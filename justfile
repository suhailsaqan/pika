set shell := ["bash", "-c"]

# List available recipes.
default:
  @just --list

# Print developer-facing usage notes (targets, env vars, common flows).
info:
  @echo "Pika: run commands + target selection"
  @echo
  @echo "iOS"
  @echo "  Simulator:"
  @echo "    just run-ios"
  @echo "  Hardware device:"
  @echo "    just run-ios --device --udid <UDID>"
  @echo "  List targets (devices + simulators):"
  @echo "    ./tools/pika-run ios list-targets"
  @echo
  @echo "  Env equivalents:"
  @echo "    PIKA_IOS_DEVICE=1               (default to device)"
  @echo "    PIKA_IOS_DEVICE_UDID=<UDID>     (pick device)"
  @echo "    PIKA_IOS_DEVELOPMENT_TEAM=...   (required for device builds)"
  @echo "    PIKA_IOS_CONSOLE=1              (attach console on device)"
  @echo "    PIKA_CALL_MOQ_URL=...           (override MOQ relay URL)"
  @echo "    PIKA_MOQ_PROBE_ON_START=1       (log QUIC+TLS probe PASS/FAIL on startup)"
  @echo
  @echo "Android"
  @echo "  Emulator:"
  @echo "    just run-android"
  @echo "  Hardware device:"
  @echo "    just run-android --device --serial <serial>"
  @echo "  List targets (emulators + devices):"
  @echo "    ./tools/pika-run android list-targets"
  @echo
  @echo "  Env equivalents:"
  @echo "    PIKA_ANDROID_SERIAL=<serial>"
  @echo "    PIKA_CALL_MOQ_URL=...           (override MOQ relay URL)"
  @echo "    PIKA_MOQ_PROBE_ON_START=1       (log QUIC+TLS probe PASS/FAIL on startup)"
  @echo
  @echo "RMP (new)"
  @echo "  Run iOS simulator:"
  @echo "    just rmp run ios"
  @echo "  Run Android emulator:"
  @echo "    just rmp run android"
  @echo "  List devices:"
  @echo "    just rmp devices list"
  @echo "  Generate bindings:"
  @echo "    just rmp bindings all"


# Run the new Rust `rmp` CLI.
rmp *ARGS:
  cargo run -p rmp-cli -- {{ARGS}}

# Smoke test `rmp init` output locally (scaffold + doctor + core check).
rmp-init-smoke NAME="rmp-smoke" ORG="com.example":
  set -euo pipefail; \
  ROOT="$PWD"; \
  BIN="$ROOT/target/debug/rmp"; \
  TMP="$(mktemp -d "${TMPDIR:-/tmp}/rmp-init-smoke.XXXXXX")"; \
  cargo build -p rmp-cli; \
  "$BIN" init "$TMP/{{NAME}}" --yes --org "{{ORG}}"; \
  cd "$TMP/{{NAME}}"; \
  "$BIN" doctor --json >/dev/null; \
  cargo check; \
  echo "ok: rmp init smoke passed ($TMP/{{NAME}})"

# End-to-end launch check for a freshly initialized project.
rmp-init-run PLATFORM="android" NAME="rmp-e2e" ORG="com.example":
  set -euo pipefail; \
  ROOT="$PWD"; \
  BIN="$ROOT/target/debug/rmp"; \
  TMP="$(mktemp -d "${TMPDIR:-/tmp}/rmp-init-run.XXXXXX")"; \
  cargo build -p rmp-cli; \
  "$BIN" init "$TMP/{{NAME}}" --yes --org "{{ORG}}"; \
  cd "$TMP/{{NAME}}"; \
  "$BIN" run {{PLATFORM}}

# Linux-safe CI checks for `rmp init` output.
rmp-init-smoke-ci ORG="com.example":
  set -euo pipefail; \
  ROOT="$PWD"; \
  BIN="$ROOT/target/debug/rmp"; \
  TMP="$(mktemp -d "${TMPDIR:-/tmp}/rmp-init-smoke-ci.XXXXXX")"; \
  cargo build -p rmp-cli; \
  "$BIN" init "$TMP/rmp-all" --yes --org "{{ORG}}" --json >/dev/null; \
  (cd "$TMP/rmp-all" && cargo check >/dev/null); \
  "$BIN" init "$TMP/rmp-android" --yes --org "{{ORG}}" --no-ios --json >/dev/null; \
  (cd "$TMP/rmp-android" && cargo check >/dev/null); \
  "$BIN" init "$TMP/rmp-ios" --yes --org "{{ORG}}" --no-android --json >/dev/null; \
  (cd "$TMP/rmp-ios" && cargo check >/dev/null); \
  echo "ok: rmp init ci smoke passed"

# Nightly Linux lane: scaffold + Android emulator run.
rmp-nightly-linux NAME="rmp-nightly-linux" ORG="com.example" AVD="rmp_ci_api35":
  set -euo pipefail; \
  ROOT="$PWD"; \
  BIN="$ROOT/target/debug/rmp"; \
  TMP="$(mktemp -d "${TMPDIR:-/tmp}/rmp-nightly-linux.XXXXXX")"; \
  ABI="x86_64"; \
  IMG="system-images;android-35;google_apis;$ABI"; \
  cargo build -p rmp-cli; \
  "$BIN" init "$TMP/{{NAME}}" --yes --org "{{ORG}}" --no-ios; \
  if ! emulator -list-avds | grep -qx "{{AVD}}"; then \
    echo "no" | avdmanager create avd -n "{{AVD}}" -k "$IMG" --force; \
  fi; \
  cd "$TMP/{{NAME}}"; \
  CI=1 "$BIN" run android --avd "{{AVD}}" --verbose; \
  adb devices || true

# Nightly macOS lane: scaffold + iOS simulator run.
rmp-nightly-macos NAME="rmp-nightly-macos" ORG="com.example":
  set -euo pipefail; \
  ROOT="$PWD"; \
  BIN="$ROOT/target/debug/rmp"; \
  TMP="$(mktemp -d "${TMPDIR:-/tmp}/rmp-nightly-macos.XXXXXX")"; \
  cargo build -p rmp-cli; \
  "$BIN" init "$TMP/{{NAME}}" --yes --org "{{ORG}}" --no-android; \
  cd "$TMP/{{NAME}}"; \
  "$BIN" run ios --verbose

# Run pika_core tests.
test *ARGS:
  cargo test -p pika_core {{ARGS}}

# Check formatting (cargo fmt).
fmt:
  cargo fmt --all --check

# Lint with clippy.
clippy *ARGS:
  cargo clippy -p pika_core {{ARGS}} -- -D warnings

# CI-safe pre-merge for the Pika app lane.
pre-merge-pika: fmt
  just clippy --lib --tests
  just test --lib --tests
  cargo build -p pika-cli
  npx --yes @justinmoon/agent-tools check-docs
  npx --yes @justinmoon/agent-tools check-justfile
  @echo "pre-merge-pika complete"

# CI-safe pre-merge for the openclaw-marmot (marmotd) lane.
pre-merge-marmotd:
  cargo clippy -p marmotd -- -D warnings
  cargo test -p marmotd
  @echo "pre-merge-marmotd complete"

# CI-safe pre-merge for the RMP tooling lane.
pre-merge-rmp:
  just rmp-init-smoke-ci
  @echo "pre-merge-rmp complete"

# Single CI entrypoint for the whole repo.
pre-merge:
  just pre-merge-pika
  just pre-merge-marmotd
  just pre-merge-rmp
  @echo "pre-merge complete"

# Nightly root task.
nightly:
  just pre-merge
  just nightly-pika-e2e
  just nightly-marmotd
  @echo "nightly complete"

# Nightly E2E (Rust): run all `#[ignore]` tests (intended for long/flaky network suites).
nightly-pika-e2e:
  set -euo pipefail; \
  if [ -z "${PIKA_TEST_NSEC:-}" ]; then \
    echo "note: PIKA_TEST_NSEC not set; e2e_deployed_bot_call will skip"; \
  fi; \
  cargo test -p pika_core --tests -- --ignored --nocapture

# Nightly lane: build marmotd + run the marmotd E2E suite (local Nostr relay + local MoQ relay).
nightly-marmotd:
  just e2e-local-marmotd
  just openclaw-marmot-scenarios

# openclaw-marmot scenario suite (local Nostr relay + marmotd scenarios).
openclaw-marmot-scenarios:
  ./openclaw-marmot/scripts/phase1.sh
  ./openclaw-marmot/scripts/phase2.sh
  ./openclaw-marmot/scripts/phase3.sh
  ./openclaw-marmot/scripts/phase3_audio.sh
  MARMOT_TTS_FIXTURE=1 cargo test -p marmotd daemon::tests::tts_pcm_publish_reaches_subscriber -- --nocapture

# Full QA: fmt, clippy, test, android build, iOS sim build.
qa: fmt clippy test android-assemble ios-build-sim
  @echo "QA complete"

# Deterministic E2E: local Nostr relay + local Rust bot (iOS + Android).
e2e-local-relay:
  just ios-ui-e2e-local
  just android-ui-e2e-local

# E2E against public relays + deployed bot (nondeterministic).
e2e-public-relays:
  ./tools/ui-e2e-public --platform all

# Rust-level E2E smoke test against public relays (nondeterministic).
e2e-public:
  PIKA_E2E_PUBLIC=1 cargo test -p pika_core --test e2e_public_relays -- --ignored --nocapture

# E2E call test over the real MOQ relay (nondeterministic; requires QUIC egress).
e2e-real-moq:
  cargo test -p pika_core --test e2e_real_moq_relay -- --ignored --nocapture

# Local E2E: local Nostr relay + local marmotd daemon.
# Builds marmotd from the workspace crate (`crates/marmotd`) so no external repos are required.
e2e-local-marmotd:
  set -euo pipefail; \
  cargo build -p marmotd; \
  MARMOTD_BIN="$PWD/target/debug/marmotd" \
    PIKA_E2E_LOCAL=1 \
    cargo test -p pika_core --test e2e_local_marmotd_call -- --ignored --nocapture

# Build Rust core for the host platform.
rust-build-host:
  cargo build -p pika_core --release

# Generate Kotlin bindings via UniFFI.
gen-kotlin: rust-build-host
  mkdir -p android/app/src/main/java/com/pika/app/rust
  # Resolve the host cdylib extension (dylib on macOS, so on Linux).
  LIB=$(ls -1 target/release/libpika_core.dylib target/release/libpika_core.so target/release/libpika_core.dll 2>/dev/null | head -n 1); \
  if [ -z "$LIB" ]; then echo "Missing built library: target/release/libpika_core.*"; exit 1; fi; \
  cargo run -q -p uniffi-bindgen -- generate \
    --library "$LIB" \
    --language kotlin \
    --out-dir android/app/src/main/java \
    --no-format \
    --config rust/uniffi.toml

# Cross-compile Rust core for Android (arm64, armv7, x86_64).
android-rust:
  mkdir -p android/app/src/main/jniLibs
  cargo ndk -o android/app/src/main/jniLibs \
    -P 26 \
    -t arm64-v8a -t armeabi-v7a -t x86_64 \
    build -p pika_core --release

# Write android/local.properties with SDK path.
android-local-properties:
  SDK="${ANDROID_HOME:-${ANDROID_SDK_ROOT:-}}"; \
  if [ -z "$SDK" ]; then echo "ANDROID_HOME/ANDROID_SDK_ROOT not set (run inside nix develop)"; exit 1; fi; \
  printf "sdk.dir=%s\n" "$SDK" > android/local.properties

# Build Android debug APK.
android-assemble: gen-kotlin android-rust android-local-properties
  cd android && ./gradlew :app:assembleDebug

# Build and install Android debug APK on connected device.
android-install: gen-kotlin android-rust android-local-properties
  cd android && ./gradlew :app:installDebug

# Run Android instrumentation tests (requires running emulator/device).
android-ui-test: gen-kotlin android-rust android-local-properties
  ./tools/android-ensure-debug-installable
  SERIAL="$(./tools/android-pick-serial)"; \
  ANDROID_SERIAL="$SERIAL" cd android && ./gradlew :app:connectedDebugAndroidTest

# Android E2E: local Nostr relay + local Rust bot. Requires emulator.
android-ui-e2e-local:
  ./tools/ui-e2e-local --platform android

# Android E2E: public relays + deployed bot (nondeterministic). Requires emulator.
android-ui-e2e:
  ./tools/ui-e2e-public --platform android

# Generate Swift bindings via UniFFI.
ios-gen-swift: rust-build-host
  mkdir -p ios/Bindings
  cargo run -q -p uniffi-bindgen -- generate \
    --library target/release/libpika_core.dylib \
    --language swift \
    --out-dir ios/Bindings \
    --config rust/uniffi.toml
  python3 -c 'from pathlib import Path; import re; p=Path("ios/Bindings/pika_core.swift"); data=p.read_text(encoding="utf-8").replace("\r\n","\n").replace("\r","\n"); data=re.sub(r"[ \t]+$", "", data, flags=re.M); data=data.rstrip("\n")+"\n"; p.write_text(data, encoding="utf-8")'

# Cross-compile Rust core for iOS (device + simulator).
ios-rust:
  # Nix shells often set CC/CXX/SDKROOT/MACOSX_DEPLOYMENT_TARGET for macOS builds.
  # For iOS targets, force Xcode toolchain compilers + iOS SDK roots.
  set -euo pipefail; \
  DEV_DIR="$(./tools/xcode-dev-dir)"; \
  TOOLCHAIN_BIN="$DEV_DIR/Toolchains/XcodeDefault.xctoolchain/usr/bin"; \
  CC_BIN="$TOOLCHAIN_BIN/clang"; \
  CXX_BIN="$TOOLCHAIN_BIN/clang++"; \
  AR_BIN="$TOOLCHAIN_BIN/ar"; \
  RANLIB_BIN="$TOOLCHAIN_BIN/ranlib"; \
  IOS_MIN="17.0"; \
  SDKROOT_IOS="$(DEVELOPER_DIR="$DEV_DIR" /usr/bin/xcrun --sdk iphoneos --show-sdk-path)"; \
  SDKROOT_SIM="$(DEVELOPER_DIR="$DEV_DIR" /usr/bin/xcrun --sdk iphonesimulator --show-sdk-path)"; \
  base_env=(env -u LIBRARY_PATH -u SDKROOT -u MACOSX_DEPLOYMENT_TARGET -u CC -u CXX -u AR -u RANLIB \
    DEVELOPER_DIR="$DEV_DIR" CC="$CC_BIN" CXX="$CXX_BIN" AR="$AR_BIN" RANLIB="$RANLIB_BIN" IPHONEOS_DEPLOYMENT_TARGET="$IOS_MIN" \
    CARGO_TARGET_AARCH64_APPLE_IOS_LINKER="$CC_BIN" \
    CARGO_TARGET_AARCH64_APPLE_IOS_SIM_LINKER="$CC_BIN"); \
  "${base_env[@]}" SDKROOT="$SDKROOT_IOS" RUSTFLAGS="-C linker=$CC_BIN -C link-arg=-miphoneos-version-min=$IOS_MIN" cargo build -p pika_core --release --lib --target aarch64-apple-ios; \
  "${base_env[@]}" SDKROOT="$SDKROOT_SIM" RUSTFLAGS="-C linker=$CC_BIN -C link-arg=-mios-simulator-version-min=$IOS_MIN" cargo build -p pika_core --release --lib --target aarch64-apple-ios-sim

# Build PikaCore.xcframework (device + simulator slices).
ios-xcframework: ios-gen-swift ios-rust
  rm -rf ios/Frameworks/PikaCore.xcframework ios/.build
  mkdir -p ios/.build/headers ios/Frameworks
  cp ios/Bindings/pika_coreFFI.h ios/.build/headers/pika_coreFFI.h
  cp ios/Bindings/pika_coreFFI.modulemap ios/.build/headers/module.modulemap
  ./tools/xcode-run xcodebuild -create-xcframework \
    -library target/aarch64-apple-ios/release/libpika_core.a -headers ios/.build/headers \
    -library target/aarch64-apple-ios-sim/release/libpika_core.a -headers ios/.build/headers \
    -output ios/Frameworks/PikaCore.xcframework

# Generate Xcode project via xcodegen.
ios-xcodeproj:
  cd ios && rm -rf Pika.xcodeproj && xcodegen generate

# Build iOS app for simulator.
ios-build-sim: ios-xcframework ios-xcodeproj
  ./tools/xcode-run xcodebuild -project ios/Pika.xcodeproj -scheme Pika -configuration Debug -sdk iphonesimulator -derivedDataPath ios/build build ARCHS=arm64 ONLY_ACTIVE_ARCH=YES CODE_SIGNING_ALLOWED=NO PRODUCT_BUNDLE_IDENTIFIER="${PIKA_IOS_BUNDLE_ID:-com.justinmoon.pika.dev}"

# Run iOS UI tests on simulator (skips E2E deployed-bot test).
ios-ui-test: ios-xcframework ios-xcodeproj
  udid="$(./tools/ios-sim-ensure | sed -n 's/^ok: ios simulator ready (udid=\(.*\))$/\1/p')"; \
  if [ -z "$udid" ]; then echo "error: could not determine simulator udid"; exit 1; fi; \
  ./tools/xcode-run xcodebuild -project ios/Pika.xcodeproj -scheme Pika -derivedDataPath ios/build -destination "id=$udid" test ARCHS=arm64 ONLY_ACTIVE_ARCH=YES CODE_SIGNING_ALLOWED=NO PRODUCT_BUNDLE_IDENTIFIER="${PIKA_IOS_BUNDLE_ID:-com.justinmoon.pika.dev}" \
    -skip-testing:PikaUITests/PikaUITests/testE2E_deployedRustBot_pingPong

# iOS E2E: local Nostr relay + local Rust bot.
ios-ui-e2e-local:
  ./tools/ui-e2e-local --platform ios

# iOS E2E: public relays + deployed bot (nondeterministic). Requires PIKA_UI_E2E=1.
ios-ui-e2e:
  ./tools/ui-e2e-public --platform ios

# Optional: device automation (npx). Not required for building.
device:
  ./tools/agent-device --help

# Show Android manual QA instructions.
android-manual-qa:
  @echo "Manual QA prompt: prompts/android-agent-device-manual-qa.md"
  @echo "Tip: run `npx --yes agent-device --platform android open com.justinmoon.pika.dev` then follow the prompt."

# Show iOS manual QA instructions.
ios-manual-qa:
  @echo "Manual QA prompt: prompts/ios-agent-device-manual-qa.md"
  @echo "Tip: run `./tools/agent-device --platform ios open com.justinmoon.pika.dev` then follow the prompt."

# Build, install, and launch Android app on emulator/device.
run-android *ARGS:
  ./tools/pika-run android run {{ARGS}}

# Build, install, and launch iOS app on simulator/device.
run-ios *ARGS:
  ./tools/pika-run ios run {{ARGS}}

# Check iOS dev environment (Xcode, simulators, runtimes).
doctor-ios:
  ./tools/ios-runtime-doctor

# Interop baseline: local Rust bot. Requires ~/code/marmot-interop-lab-rust.
interop-rust-baseline:
  ./tools/interop-rust-baseline

# Interactive interop test (manual send/receive with local bot).
interop-rust-manual:
  ./tools/interop-rust-baseline --manual

# ── pika-cli (Marmot protocol CLI) ──────────────────────────────────────────

# Build pika-cli (debug).
cli-build:
  cargo build -p pika-cli

# Build pika-cli (release).
cli-release:
  cargo build -p pika-cli --release

# Show (or create) an identity in the given state dir.
cli-identity STATE_DIR=".pika-cli" RELAY="ws://127.0.0.1:7777":
  cargo run -p pika-cli -- --state-dir {{STATE_DIR}} --relay {{RELAY}} identity

# Quick smoke test: two users, local relay, send+receive.
# Requires a Nostr relay running at RELAY (e.g. `strfry` or `nostr-rs-relay`).
cli-smoke RELAY="ws://127.0.0.1:7777":
  ./tools/cli-smoke --relay {{RELAY}}
